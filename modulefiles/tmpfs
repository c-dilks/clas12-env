#%Module5.1

module-whatis find an alternative for /tmp (e.g., when it's mounted noexec)

source [file dirname $ModulesCurrentModulefile]/util/functions.tcl

set failed {}

proc settmp {tmpdir} {
    if { $tmpdir != "" } {
        if { [file writable [file dirname $tmpdir]] } {
            setenv TMPDIR $tmpdir
            setenv APPTAINER_CACHEDIR $tmpdir/apptainer.cache
            setenv APPTAINER_TMPDIR $tmpdir/apptainer.tmp
            append-path --delim " " MAVEN_OPTS -Dmaven.repo.local=$tmpdir/maven.m2
            append-path --delim " " MAVEN_OPTS -Djansi.tmpdir=$tmpdir/jansi.tmpdir
            append-path --delim " " JAVA_OPTS -Djava.io.tmpdir=$tmpdir/java.io.tmpdir
            append-path --delim " " JAVA_OPTS -Dorg.sqlite.tmpdir=$tmpdir/org.sqlite.tmpdir
            append-path --delim " " _JAVA_OPTIONS -Djava.io.tmpdir=$tmpdir/java.io.tmpdir
            append-path --delim " " _JAVA_OPTIONS -Dorg.sqlite.tmpdir=$tmpdir/org.sqlite.tmpdir
            if [module-info mode load] {
                file mkdir $::env(APPTAINER_CACHEDIR) $::env(APPTAINER_TMPDIR)
                file mkdir $::env(TMPDIR)/maven.m2 $::env(TMPDIR)/jansi.tmpdir
                file mkdir $::env(TMPDIR)/java.io.tmpdir $::env(TMPDIR)/org.sqlite.tmpdir
            }
            return 1
        }
        lappend ::failed $tmpdir
    }
    return 0
}

if {! [settmp /scratch2/$env(USER)/tmp] } {
    if {! [settmp [getenv SLURM_JOB_WORK_DIR {}]/tmp] } {
        if {! [settmp /work/clas12/2users/$env(USER)/tmp ] } {
            reportError "Could not find a /tmp replacement at any of:"
            puts stderr \t[join $failed \n\t]
            exit
        }
    }
}

